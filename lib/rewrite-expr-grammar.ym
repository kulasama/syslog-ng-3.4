/*
 * Copyright (c) 2002-2010 BalaBit IT Ltd, Budapest, Hungary
 * Copyright (c) 1998-2010 Bal√°zs Scheidler
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * As an additional exemption you are allowed to compile & link against the
 * OpenSSL libraries as published by the OpenSSL project. See the file
 * COPYING for details.
 *
 */

%code requires {

#include "rewrite-expr-parser.h"

}

%code {

#include "syslog-names.h"
#include "rewrite-expr-grammar.h"
#include "logrewrite.h"
#include "plugin.h"
#include "filter.h"
#include "filter-expr-parser.h"

#include <string.h>

LogRewrite *last_rewrite;

static gboolean
is_sdata_valid(const gchar *buffer)
{
  gboolean result = FALSE;
  const gchar *dot;
  int dot_found = 0;
  if (strlen(buffer)>6 && strncmp(buffer,".SDATA.",7)==0)
    {
      dot = memchr(buffer,'.',strlen(buffer));
      while (dot && strlen(dot)>1)
        {
          dot_found++;
          dot++;
          dot = memchr(dot, '.',strlen(dot));
        }
      if (dot_found >= 3)
        result = TRUE;
    }
  return result;
}

}

%name-prefix "rewrite_expr_"
%lex-param {CfgLexer *lexer}
%parse-param {CfgLexer *lexer}
%parse-param {LogExprNode **result}
%parse-param {gpointer arg}

%type   <ptr> rewrite_expr
%type   <ptr> rewrite_expr_list

/* INCLUDE_DECLS */

%token KW_CONDITION

%%

start
        : rewrite_expr_list log_last_junction        { *result = log_expr_node_append_tail($1, $2); if (yychar != YYEMPTY) { cfg_lexer_unput_token(lexer, &yylval); } YYACCEPT; }
	;

rewrite_expr_list
        : rewrite_expr semicolons rewrite_expr_list  { $$ = log_expr_node_append_tail(log_expr_node_new_pipe($1, &@1), $3); }
        | log_junction semicolons rewrite_expr_list      { $$ = log_expr_node_append_tail($1,  $3); }
        |                                           { $$ = NULL; }
        ;

rewrite_expr
        : KW_SUBST '(' string string
          {
            last_rewrite = log_rewrite_subst_new($4);
            free($4);
          }
          rewrite_subst_opts ')'
          {
            CHECK_ERROR(log_rewrite_subst_set_regexp(last_rewrite, $3), @3, "Error compiling regular expression");
            free($3);
            $$ = last_rewrite;
          }
        | KW_SET '(' string
          {
            last_rewrite = log_rewrite_set_new($3);
            free($3);
          }
          rewrite_expr_opts ')'                 { $$ = last_rewrite; }
        | LL_IDENTIFIER
          {
            Plugin *p;
            gint context = LL_CONTEXT_REWRITE;

            p = plugin_find(configuration, context, $1);
            CHECK_ERROR(p, @1, "%s plugin %s not found", cfg_lexer_lookup_context_name_by_type(context), $1);

            last_rewrite = (LogRewrite *) plugin_parse_config(p, configuration, &@1, NULL);
            free($1);
            if (!last_rewrite)
              {
                YYERROR;
              }
            $$ = last_rewrite;
          }
        ;

rewrite_subst_opts
        : rewrite_expr_opt rewrite_subst_opts
        | rewrite_subst_opt rewrite_subst_opts
        |
        ;

rewrite_subst_opt
        : KW_TYPE '(' string ')'
          {
            CHECK_ERROR((strcmp($3, "glob") != 0), @3, "Rewrite rules do not support glob expressions");
            log_rewrite_subst_set_matcher(last_rewrite, log_matcher_new($3));
            free($3);
          }
        | KW_FLAGS '(' regexp_option_flags ')' { log_rewrite_subst_set_flags(last_rewrite, $3); }
        ;

rewrite_expr_opts
        : rewrite_expr_opt rewrite_expr_opts
        |
        ;

rewrite_expr_opt
        : KW_VALUE '(' string ')'
          {
            const gchar *p = $3;
            if (p[0] == '$')
              {
                msg_warning("Value references in rewrite rules should not use the '$' prefix, those are only needed in templates",
                            evt_tag_str("value", $3),
                            NULL);
                p++;
              }
            last_rewrite->value_handle = log_msg_get_value_handle(p);
            if (log_msg_is_handle_macro(last_rewrite->value_handle))
              {
                msg_warning("Macros are read-only, they cannot be changed in rewrite rules, falling back to MESSAGE instead",
                            evt_tag_str("macro", p),
                            NULL);
                last_rewrite->value_handle = LM_V_MESSAGE;
              }

            /* SDATA validation */
            if (strlen($3)>6 && strncmp($3,".SDATA.",7)==0)
              {
                if (!is_sdata_valid($3))
                  {
                    msg_error("Invalid structured data format",evt_tag_str("SDATA",$3), NULL);
                    YYERROR;
                  }
              }
            free($3);
          }
        | KW_CONDITION '('
          {
            FilterExprNode *filter_expr;

            CHECK_ERROR(cfg_parser_parse(&filter_expr_parser, lexer, (gpointer *) &filter_expr, NULL), @1, NULL);
            log_rewrite_set_condition(last_rewrite, filter_expr);
          } ')'
        ;

/* INCLUDE_RULES */

%%
